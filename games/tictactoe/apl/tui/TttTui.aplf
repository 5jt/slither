 TttTui;dsl;box;board;player;ind;s
 ⍝ Play one game of tictactoe (TUI)
 dsl←##.dsl
 box←{rule⍪('|',⍵,'|')⍪rule←1⌽'++','-'⍴⍨1↓⍴⍵}
 board←3 3⍴' '
 :Repeat
     ⎕←box board
     player←dsl.whonext board
     :If dsl.lastmove board
         ⎕←'Only one cell left!'
         ind←dsl.forced board
     :ElseIf player='X'
         ind←pickmove board ⍝ ⍬ for quit
     :Else
         ind←⎕IO+'O'dsl.best board
     :EndIf

     :If ind≡⍬
         board←'quit'
     :Else
         (ind⌷board)←player
     :EndIf
 :Until 0≠s←dsl.status board

 :If s≠¯2 ⍝ quit
     ⎕←box board
 :EndIf
 ⎕←'OK, later.' 'A draw!' 'You won!' 'I won!'⊃⍨¯2 ¯1 1 2⍳s

 ⎕←'Thanks for playing.'
