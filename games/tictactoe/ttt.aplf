 ttt;board;box;ind;str;xxx;s
 ⍝ Play one game of tictactoe (TTY)
 box←{rule⍪('|',⍵,'|')⍪rule←1⌽'++','-'⍴⍨2⊃⍴⍵}
 board←3 3⍴' '
 :Repeat
     ⎕←box board
     :If lastmove board
         board←'X'forced board
     :Else
         str←{⍞←⍵ ⋄ (≢⍵)↓⍞}'Row and column? or (Q to quit) '
         :If 'q'=⎕C⊃str
             board←'quit'
         :ElseIf (1 1≢⊃(xxx ind)←⎕VFI str)
         :OrIf 0∊ind∊1 2 3
             ⎕←'Just two digits (1, 2 or 3) please.'
         :ElseIf ' '≠ind⌷board
             ⎕←'Sorry, already taken!'
         :Else
             (ind⌷board)←'X'            ⍝ You
         :EndIf

         :If 0=status board
             :If lastmove board
                 board←'O'forced board
             :Else
                 ind←⎕IO+'O'best board
                 (ind⌷board)←'O'        ⍝ Me
             :EndIf
         :EndIf

     :EndIf
 :Until 0≠s←status board

 :If s≠¯2
     ⎕←box board
 :EndIf
 ⎕←'OK, later.' 'A draw!' 'You won!' 'I won!'⊃⍨¯2 ¯1 1 2⍳s

 ⎕←'Thanks for playing.'
